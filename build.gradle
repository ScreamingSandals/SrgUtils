buildscript {
    repositories {
        mavenLocal()
        maven {
            url = 'https://maven.minecraftforge.net/'
        }
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'net.minecraftforge.gradleutils' version '1.+'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

group 'net.minecraftforge'
// version = gradleutils.getTagOffsetVersion()
version = '0.4.11-SNAPSHOT'

// java.toolchain.languageVersion = JavaLanguageVersion.of(8)

ext {
    CHANGELOG = rootProject.file('build/changelog.txt')
}

repositories {
    mavenLocal()
    maven { url = 'https://maven.minecraftforge.net/' }
    mavenCentral()
}

test {
    useJUnitPlatform()
}

license {
    header = file('LICENSE-header.txt')
    ext {
        project = 'SRG Utils'
    }
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.8.2')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.8.2')
    testImplementation('org.powermock:powermock-core:2.0.9')
    compileOnly('com.google.code.findbugs:jsr305:3.0.2')
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            if (CHANGELOG.exists()) {
                artifact(CHANGELOG) {
                    classifier = 'changelog'
                }
            }
            artifact jar
            artifact sourcesJar
            pom {
                name = 'SRG Utils'
                description = 'Library for managing java obfuscation mappings in various formats.'
                url = 'https://github.com/MinecraftForge/SrgUtils'
                scm {
                    url = 'https://github.com/MinecraftForge/SrgUtils'
                    connection = 'scm:git:git://github.com/MinecraftForge/SrgUtils.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/SrgUtils.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/SrgUtils/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'LexManos'
                        name = 'LexManos'
                    }
                }
            }
        }
    }
    repositories {
        maven({ MavenArtifactRepository repository ->
            if (((String) project.version).contains("SNAPSHOT")) {
                repository.url = System.getenv("NEXUS_URL_SNAPSHOT")
            } else {
                repository.url = System.getenv("NEXUS_URL_RELEASE")
            }
            repository.credentials.username = System.getenv("NEXUS_USERNAME")
            repository.credentials.password = System.getenv("NEXUS_PASSWORD")
        })
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

